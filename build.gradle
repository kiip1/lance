plugins {
    id 'java'
    id 'maven-publish'
    id 'edu.sc.seis.launch4j' version '2.5.0'
}

group 'nl.kiipdevelopment'
version '1.0.10'
targetCompatibility = '11'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains:annotations:20.1.0'
    implementation 'com.google.code.gson:gson:2.8.0'
}

task standaloneJar(type: Jar) {
    baseName = 'Lance-standalone'

    manifest {
        attributes(
                'Main-Class': 'nl.kiipdevelopment.lance.Main'
        )
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    with jar
}

launch4j {
    headerType = 'console'
    mainClassName = 'nl.kiipdevelopment.lance.Main'
    outfile = 'Lance.exe'
    jarTask = project.tasks.standaloneJar
}

jar.finalizedBy(standaloneJar)

publishing {
    repositories {
        maven {
            url repoWriteUrl
        }
    }

    publications {
        maven(MavenPublication) {
            artifactId = 'lance'

            from components.java
        }
    }
}